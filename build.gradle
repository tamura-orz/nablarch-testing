// -*- mode: groovy; coding: utf-8-unix -*-


group = 'com.nablarch.framework'
version = '1.0.6'
description = 'テスティングフレームワーク'


repositories {
  mavenLocal()
  maven { url nablarchRepoUrl }
  jcenter()
  
  // サードパーティ製ライブラリの格納リポジトリ
  if(project.hasProperty('nablarchExtRepoUrl')) {
     maven { url "${nablarchExtRepoUrl}" }
   }

}

buildscript {
  repositories {
    mavenLocal()
    maven { url nablarchRepoUrl }
    jcenter()
  
    // サードパーティ製ライブラリの格納リポジトリ
    if(project.hasProperty('nablarchExtRepoUrl')) {
       maven { url "${nablarchExtRepoUrl}" }
     }

  }
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:${coberturaPluginVersion}"
    classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
  }
}

apply {
  plugin 'com.nablarch.dev.nablarch-build'
  plugin 'com.nablarch.dev.nablarch-sonarqube'
  plugin 'com.nablarch.dev.nablarch-maven-deploy'
  plugin 'cobertura'
  plugin "io.spring.dependency-management"
}

configurations {
  mock
  cobertura
}

dependencyManagement {
  imports {
    mavenBom 'com.nablarch.profile:nablarch-bom:5u6'
  }
  // applyMavenExclusions false
}

sourceSets.test.compileClasspath = configurations.mock + sourceSets.test.compileClasspath
sourceSets.test.runtimeClasspath = configurations.mock + sourceSets.test.runtimeClasspath

dependencies {
  // nablarch
  // これらのモジュール内部のクラスを、コンポーネント定義ファイルにコンポーネントを定義せずに直接呼び出している。
  // このため、nablarch-testingの依存ライブラリとして明示的に定義しておく(プロジェクト側でpomに定義する必要がなくなる)
  // ※コンポーネント定義ファイルに定義した場合は、プロジェクトのpom(build.gradle)に依存ライブラリとして追加されているはずなので、
  // nablarch-testingの依存ライブラリとする必要はない
  compile 'com.nablarch.framework:nablarch-common-dao'
  compile 'com.nablarch.framework:nablarch-fw-web-extension'

  mock('org.jmockit:jmockit:1.13')
  provided 'com.nablarch.profile:nablarch-all-in-one:5u6'
  testCompile 'com.nablarch.framework:nablarch-backward-compatibility:1.0.1'
  // messaging
  compile 'org.apache.activemq:activemq-all:5.4.2'
  provided 'javax.jms:jms-api:1.1-rev-1'

  compile 'org.mortbay.jetty:jetty:6.1.24'

  // findbugs
  runtime 'xml-apis:xml-apis:1.0.b2'
  compile('com.google.code.findbugs:findbugs:1.3.9') {
    exclude module: 'xercesImpl' // Xercesが使われないようにする
    exclude module: 'xom'
  }

  // compile 'org.apache.poi:poi:3.8'
  compile 'org.apache.poi:poi-ooxml:3.8'
  compile 'junit:junit:4.10'

  runtime 'org.mortbay.jetty:jsp-2.1-glassfish:2.1.v20100127'
  runtime 'org.mortbay.jetty:jsp-api-2.1-glassfish:2.1.v20100127'


  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'

  // JDBC Drivers
  testCompile (group: 'com.oracle', name: 'ojdbc6', version: '11.2.0.4.0')
  testCompile (group: 'com.oracle', name: 'ucp', version: '11.2.0.3.0')
  testRuntime (group: 'com.ibm', name: 'db2jcc4', version: '9.7.200.358')
  testRuntime (group: 'org.postgresql', name: 'postgresql', version: '9.4-1201-jdbc4')
  testRuntime (group: 'com.microsoft', name: 'sqljdbc4', version: '4.0')

  // JPA
  testCompile (group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.5.1')

  // HereIs
  testCompile 'com.nablarch.dev:nablarch-test-support-hereis:1.0.0'
  testCompile (group: 'com.nablarch.tool', name: 'nablarch-toolbox', version: '1.0.2')
  testRuntime files("${projectDir}/src/test/java/")
  
  // for UsageOfUnpublishedMethodDetectorTest
  testRuntime (group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0')

  cobertura "net.sourceforge.cobertura:cobertura:1.9.4"
}



processTestResources {
  doFirst {
    def dbType = System.getenv()['DB_TYPE']
    if (dbType != null && dbType != '') {
      new File("${projectDir}/src/test/resources/db.config").setText(new File("${projectDir}/src/test/resources/db/" + dbType + '-db.config').getText('UTF-8'), 'UTF-8')
      new File("${projectDir}/src/test/resources/datasource.xml").setText(new File("${projectDir}/src/test/resources/db/" + dbType + '-datasource.xml').getText('UTF-8'), 'UTF-8')
    }
  }
}

test {

  jvmArgs (
          '-XX:MaxPermSize=256m',
          '-Xmx512m',
          '-Dfile.encoding=UTF-8',
          '-XX:-UseSplitVerifier'
  )

  ignoreFailures = true
  
  exclude '**/*Db2*',
          // テストの実行順に依存しているため、Suiteを利用して実行順を制御している。
          // Gradleでは、Suiteの実行対象に含まれていても、独立したクラスとしても実行されてしまうため、2重にテストが実行される。
          '**/*PublishedApisInfoTest$*Suite*', '**/*UsageOfUnpublishedMethodDetectorTest*'


  doFirst {
    def logDir = new File("${projectDir}/src/test/log")
    if (!logDir.exists()) {
      assert logDir.mkdirs()
    }
  }

  cobertura {
    coberturaVersion = '1.9.4.1'
    coverageSourceDirs = ["${projectDir}/src/main/java/"]
    //coverageIgnoreTrivial = true        // cobertura2.0以上じゃないと使えないらしい
    coverageFormats = ['xml']
    coverageDirs = [project.sourceSets.main.output.classesDir]
    coverageExcludes = [
            '.*Db2.*',
            '.*PublishedApisInfoTest\\$.*Suite.*',
            '.*UsageOfUnpublishedMethodDetectorTest.*'
    ]
  }
}

task coverage(dependsOn: ['clean', 'generateCoberturaReportByAnt'])


def coberturaReportDir = new File(project.buildDir, "cobertura-report")

task generateCoberturaReportByAnt(dependsOn: 'cobertura') {
  def dataFile = "${project.buildDir}/cobertura/cobertura.ser"

  inputs.file file(dataFile)
  outputs.dir coberturaReportDir

  doLast {
    ant {
      taskdef(resource: 'tasks.properties',
              classpath: configurations.cobertura.asPath)

      'cobertura-report'(
              destdir: coberturaReportDir,
              datafile: dataFile,
              srcdir: "${projectDir}/src/main/java/",
              format: 'xml'
      )
    }
  }
}

sonarRunner {
  sonarProperties {
    property "sonar.java.coveragePlugin", "cobertura"
    property "sonar.cobertura.reportPath", "${coberturaReportDir}/coverage.xml"
  }
}

tasks.sonarRunner {
  dependsOn = ['coverage']
}

// for open api
apply plugin: 'com.nablarch.dev.nablarch-published-api'

configurations {
  publishedApiDoc
}

dependencies {
  publishedApiDoc "com.nablarch.tool:nablarch-toolbox:1.0.2"
}

publishedApi {
  apiName = "NablarchApi"
}
generatePublishedApiDocForArchitect.dependsOn jar
generatePublishedApiDocForProgrammer.dependsOn jar

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}
